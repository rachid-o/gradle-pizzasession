plugins {
  id "net.foragerr.jmeter" version "1.0.7-3.0-BETA"
}


ext {
    reportFile = "$buildDir/pizza-rapport.xml"
}

task installPlugins(type: Copy) {
    from 'plugins'
    into "$buildDir/jmeter/lib/ext"
}

task readResult << {
    println file(reportFile).text
}

task performanceIndex(dependsOn: [jmRun]) << {
    println "Calculating average from report: ${reportFile}"
    def testResults = new XmlSlurper().parse(reportFile)

     def durations = testResults.children()
        .findAll{ it['@lb'] == 'WebSocket request-response Sampler' }
        .collect{ it.@t.text() as int}
    def avg = durations.sum() / durations.size()
    println "perfIndex: $avg"
    ext.result = avg
}

task failWhenSlow(dependsOn: [performanceIndex]) << {
    if(performanceIndex.result > 340) {
        throw new Exception("Performance too bad")
    } else {
        println "Performance OK"
    }
}

task runtests(dependsOn: [failWhenSlow, performanceIndex])


jmInit.dependsOn installPlugins

